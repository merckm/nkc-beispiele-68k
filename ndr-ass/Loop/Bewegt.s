* KLEINES BEISPIELPROGRAMM FUER DIE
* MEHRSEITENNUTZUNG FUER BEWEGTE
* FIGUREN.
* LOOP 05, Seite 7
  
 GESCHW EQU 2
  
START:
 MOVE #0,D1     * X=0   IST STARTWERT
 CLR D6         * ZAEHLER FUER WECHSEL DER FIGUREN
SCHLEIFE:
 MOVE D1,D0     * Z.B. X-KOORDINATE VERWENDEN
 JSR @SIN       * FUER Y-ACHSE, BEWEGUNG ERZEUGEN
 EXT.L D0       *
 DIVS #5,D0     * AMPLITUDE BEGRENZEN
 ADD #100,D0    * UND IN MITTE LEGEN.
 MOVE D0,D2 	* NEUE Y-KOORDINATE
 LEA MEN1,A0    * ERSTE FIGUR NEHMEN
 BTST #3,D6     * ODER ZWEITE
 BEQ WEITER
 LEA MEN2,A0    * DAMIT ERGIBT SICH WECHSEL
WEITER:
 JSR NEUFIIGUR
 ADD #GESCHW,D1 * UND IN X-RICHTUNG WEITER
WARTE:
 JSR @SYNC      * MIN 20MS WARTEN
 BEQ WARTE      * SONST KEINE GLEITENDE BEWEGUNG
 ADD #1,D6      * FUER FIGURENWECHSEL
 CMP #512,D1    * UBER DEN GESAMTEN SCHIRM WANDERN LASSEN
 BLE SCHLEIFE   *
 RTS
 
NEUFIIGUR:      * D1=X, D2=Y, A0=FIGURADR
 MOVEM.L A0/D0-D2,-(A7)
 MOVE D1,XT1    * XT1,YT1 UND ADRT1 SIND DIE
 MOVE D2,YT1    * WERTE FUER DAS NEUE BILD
 MOVE.L A0,ADRT1        * ZUNAECHST NEUE BILDSEITE ANWAEHLEN
 MOVE SCHREIBSEITE,D0
 MOVE LESESEITE,D1
 JSR @NEWPAGE
 EOR #1,SCHREIBSEITE    * DANN WIRD SCHREIB- UND LESESEITE
 EOR #1,LESESEITE       * FUER DEN NAECHSTEN AUFRUF VERTAUSCHT
 MOVE XT3,D1            * NUN WIRD DAS ALTE BILD GELOESCHT
 MOVE YT3,D2            * ES LIEGT ZWEI BILDER ZURUECK,
 MOVEA.L ADRT3,A0       * DAHER STEHEN DIE WERTE IN ..T3
 BSR LOESCHE
 MOVE XT1,D1            * DAS NEUE BILD KANN NUN GESCHRIBEN WERDEN
 MOVE YT1,D2            * ES STEHT IN ..T1
 MOVEA.L ADRT1,A0
 BSR SCHREIBE
 MOVE XT2,XT3           * SCHLIESSLICH MUSS ALLES UM EINE
 MOVE XT1,XT2           * SEITEN1HEIT VERSCHOBEN WERDEN,
 MOVE YT2,YT3           * DAMIT SPAETER DAS LOESCHEN KORREKT
 MOVE YT1,YT2           * DURCHEFUEHRT WIRD.
 MOVE.L ADRT2,ADRT3
 MOVE.L ADRT1,ADRT2
 MOVEM.L (A7)+,A0/D0-D2
 RTS
 
TEST:           * AUSGABE BEIDER FIGUREN AUF DEM SCHIRM
 LEA MEN1,A0    * DAMIT TEST MOEGLICH OB DATEN RICHTIG
 MOVE #100,D1   * EINGETRAGEN WURDEN.
 MOVE #100,D2
 BSR SCHREIBE
 LEA MEN2,A0
 MOVE #150,D1
 MOVE #100,D2
 BSR SCHREIBE
 RTS
 
SCHREIBE:       * A0=ADRESSE
 MOVE.B #$11,D0 * SCHRIFTGROESSE, FALLS TEXTE DA
 MOVEM.L A0/D1-D2,-(A7)
 JSR @SETPEN    * D1=X,D2=Y
 JSR @CMDPRINT  * GRUNDPROGRAMM
 MOVEM.L (A7)+,A0/D1-D2
 RTS
 
LOESCHE:        * A0=ADRESSE
 MOVE.B #$11,D0 * SCHRIFTGROESSE, FALLS TEXTE DA
 MOVEM.L A0/D1-D2,-(A7)
 JSR @ERAPEN    * D1=X,D2=Y
 JSR @CMDPRINT
 MOVEM.L (A7)+,A0/D1-D2
 RTS
 
 
* FIGUREN IN KURZVEKTOREN CODIERT.
 
MEN1: DC.B $F9,$FD,$F9,$FD,$F9,$FD,$F9,$FD
      DC.B $FA,$FA,$FA,$FA
      DC.B $FB,$FB
      DC.B $FE,$FE,$FE,$FE
      DC.B $FF,$FF
      DC.B $FC,$FC,$FC,$FC
      DC.B 3            * PEN UP
      DC.B $FA,$FA,$FA,$F8
      DC.B 2            * PEN DOWN
      DC.B $F8,$FA,$FE,$FC
      DC.B 0            * PEN ENDE
 
MEN2: DC.B $FA,$FD,$F9,$FD,$F9,$FD,$F9,$FD,$F9
      DC.B $FC,$FA,$FA,$FA,$FA
      DC.B $FB,$FB
      DC.B $FE,$FE,$FE,$FE
      DC.B $FF,$FF
      DC.B $FC,$FC,$FC,$FC
      DC.B 3            * PEN UP
      DC.B $FA,$FA,$FA,$F8
      DC.B $F8,$F8,$F8,$F8,$F8
      DC.B 2            * PEN DOWN
      DC.B $F8,$FA,$FE,$FC
      DC.B 0            * PEN ENDE
 
* RAM SPEICHER, ACHTUNG, WERTE RICHTIG EINTRAGEN.
    DS 0        * AUF WORTGRENZE LEGEN.
 
XT1: DC.W 0     * X-KOORDINATE  ZEITPUNKT T1
XT2: DC.W 0
XT3: DC.W 0
YT1: DC.W 0     * Y-KOORDINATE  ZEITPUNKT T1
YT2: DC.W 0
YT3: DC.W 0
ADRT1: DC.L MEN1  * FIGUR-ADRESSE ZEITPUNKT T1
ADRT2: DC.L MEN1  * ACHTUNG, DEFAULT WERTE
ADRT3: DC.L MEN1
SCHREIBSEITE: DC.W 1
LESESEITE: DC.W 0
 
  
END