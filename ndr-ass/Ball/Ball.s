; ***********************************
;* SIMULATION DES FLUG EINES BALLES *
; ***********************************
 
 ORG $9C00
 
; ***** VEREINBARUNGS-BAUSTEIN *****
BALL:
 DC.B 0,1,2,3,4,5,6,7,10
 
 DS 0
 
X: DC.W 0       * X-KOORDINATE
Y: DC.W 0       * Y-KOORDINATE
VX: DC.W 0      * GESCHWINDIGKEIT X-RICHTUNG
VY: DC.W 0      * GESCHWINDIGKEIT Y-RICHTUNG
T: DC.W 0       * ABGELAUFENE ZEIT, FUER REIBUNG
 
* PROGRAMMBAUSTEIN
 
START:
 CLR X          * START BEI X=0
 MOVE #250,Y    * UND Y=250
 CLR VX         * WIRD DORT FALLENGELASSEN
 MOVE #3,VX     * ABER MIT ANFANGSGESCHW.
SCHLEIFE:
 MOVE X,D1      * BALL DARSTELLEN
 MOVE Y,D2      * 
 MOVE #3,D0     * GROESSE
 LEA BALL,A0    * ADRESSE DES BALLS
 JSR @FIGUR     * UND DARSTELLEN
WARTE:          * DADURCH ZEITRASTER
 JSR @SYNC
 BEQ WARTE
 ADD #1,T       * ZEIT ERHOEHEN
 MOVE VX,D0
 ADD D0,X       * NEUE X-KOORDINATE
 SUB #1,VY      * GRAVITATION
 MOVE VY,D0
 ADD D0,Y       * NEUE Y-KOORDINATE
 BPL SCHLEIFE   * FALLS UEBER BODEN, DANN OK
 SUB D0,Y       * SONST REFLEXION
 ADD #1,VY      * VEKTOR GENAU UMGEKEHRT
 NEG VY         * NACH OBEN GERICHTET, PHYS: NICHT GANZ EXAKT
 SUB #1,VY      * ZUSAETZLICH REIBUNG
 CMP #40,T      * ALLE 40*20MS
 BLE SCHLEIFE
 CLR T          * NEU ZAEHLEN
 SUB #1,VX
 BPL SCHLEIFE
 CLR VX         * REIBUNG NUR IN EINER RICHTUNG WIRKSAM
 BRA SCHLEIFE   * ENDLOS
