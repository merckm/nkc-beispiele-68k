*************************
* STANDUHRPROGAMM   1.0 *
*************************
* AUF ADRESSE $A000 IM EDITOR EINGEBEN
* CODE AB $9C00
 
* VEREINBARUNGS-BAUSTEIN
 
GDP EQU $FFFFFF70       * STATUSPORT GDP
 
PENDELPOS:  DC.W 0      * PENDELPOSITION 0,2,4,6,...,18 ALS INDEX
RICHTUNG:   DC.W 2      * PENDELRICHTUNG UND GESCHWINDIGKEIT
MINWINKEL:  DC.W 0      * 0..359 GRAD   6 GRAD PRO MINUTE
STDWINKEL:  DC.W 90     * 0..359 GRAD  30 GRAD PRO STUNDE
MINZAEHLER: DC.W 0      * 0..9
STDZAEHLER: DC.W 0      * 0..119
 
PENTAB:
 DC.W -10,-9,-8,-5,-2,2,5,8,9,10
 
* UNTERPROGRAMM-BAUSTEIN
 
SEK10TEL:               * UNTERPROGRAMM 1 SEK
 MOVE #5-1,D3           * 1/10 SEK. AUSZAEHLEN
WARTE:                  * DURCHLAUF IST 20MS
 JSR @SYNC              * UNTERPROG, LIEFERT
 BEQ WARTE              * ALLE 20MS WERT <> 0
 DBRA D3,WARTE          * DAS GANZE 5 MAL
 RTS
 
ZAEHLER:                * NEUER SEKUNDENTAKT
 JSR @ERAPEN            * ALLE ZAEHLER AUF NEUEN STAND
 BSR ZEIGERAUS          * ABSCHALTEN DER ZEIGER
 ADD #1,MINZAEHLER      * ZAEHLT ALLE SEKUNDE
 CMP #10,MINZAEHLER     * VON 0 BIS 9
 BLT ZAE1:
 CLR MINZAEHLER
 ADD #1,MINWINKEL       * 1 GRAD ENTSPRICHT 10 SEKUNDEN
 CMP #360,MINWINKEL
 BLT ZAE1
 CLR MINWINKEL
ZAE1:
 ADD #1,STDZAEHLER      * ZAEHLT SEKUNDEN, VON 0..119
 CMP #120,STDZAEHLER
 BLT ZAE2
 CLR STDZAEHLER
 ADD #1,STDZAEHLER      * 1 GRAD ENTSPRICHT 120 SEKUNDEN
 CMP #360,STDWINKEL     *
 BLT ZAE2
 CLR STDWINKEL
ZAE2:
 JSR @SETPEN            * NEUE ZEIGERSTELLUNG AUSGEBEN
 BSR ZEIGERAUS
 RTS
 
PENDEL:                 * MIT SCHILDKROETEN-
 MOVE #100,D0           * GRAPHIK, PENDEL DARSTELLEN
 JSR @SCHREITE
 MOVE #-90,D0           * PERPENDIKEL ORIENTIERT
 JSR @DREHE
 MOVE #10-1,D3
PENWDH:
 MOVE #3,D0
 JSR @SCHREITE
 MOVE #36,D0
 JSR @DREHE
 DBRA D3,PENWDH
 RTS
 
PENDELAUS:              * AUF AKTUELLER POSITION AUSGEBEN
 MOVE #256,D1           * JE NACH WINKEL
 MOVE #256,D2
 LEA PENTAB,A0
 MOVE PENDELPOS,D3
 ADDA.W D3,A0
 MOVE #270,D3           * MITTELPOSITION BEI 9
 ADD (A0),D3
 JSR @SET
 JSR @HIDE              * SCHILDKROETE SELBST AUSSCHALTEN
 BSR PENDEL
 RTS
 
ZAEPENDEL:              * ALLE 1/10 SEK NEUE PENDELPOSITION
 JSR @ERAPEN
 BSR PENDELAUS          * ALTES LOESCHEN
 MOVE PENDELPOS,D0
 ADD RICHTUNG,D0
 MOVE D0,PENDELPOS
 CMP #18,PENDELPOS
 BLT ZAP1               * 0,2,4,6,...18 ERLAUBT
 NEG RICHTUNG           * UMDREHEN DES RICHTUNGSSINNS
ZAP1:
 CMP #0,PENDELPOS
 BGT ZAP2               * 0 IST MINIMUM
 NEG RICHTUNG           * AUS -2 WIRD 2
ZAP2:
 JSR @SETPEN
 BSR PENDELAUS          * PENDEL WIEDER DARSTELLEN
 RTS
 
ZEIGERAUS:              * ANHAND DER WINKEL, ZEIGER DARSTELLEN
 MOVE #256,D1
 MOVE #256+40,D2
 MOVE #90,D3
 SUB MINWINKEL,D3
 JSR @SET
 JSR @HIDE
 MOVE #22,D0            * MINUTENZEIGER
 JSR @SCHREITE
 MOVE #256,D1
 MOVE #256+40,D2
 MOVE #90,D3
 SUB STDWINKEL,D3
 JSR @SET
 JSR @HIDE
 MOVE #16,D0            * STUNDENZEIGER
 JSR @SCHREITE
 RTS
 
ZIFFERBLATT:            * ZIFFERNBLATT DARSTELLEN
 MOVE #256,D1
 MOVE #256+10,D2
 MOVE #0,D3
 JSR @SET
 MOVE #12-1,D3
ZIFWDH:
 MOVE #90,D0
 JSR @DREHE
 MOVE #5,D0
 JSR @SCHREITE 
 MOVE #-5,D0
 JSR @SCHREITE 
 MOVE #-90,D0
 JSR @DREHE
 MOVE #15,D0
 JSR @DREHE
 MOVE #250,D0
 JSR @SCHR16TEL 
 MOVE #15,D0
 JSR @DREHE
 DBRA D3,ZIFWDH
 RTS
 
GEHAEUSE:               * UHRENGEHAEUSE DARSTELLEN
 MOVE #90,D0
 JSR @DREHE
 MOVE #-30,D0
 JSR @SCHREITE 
 MOVE #60,D0
 JSR @SCHREITE 
 MOVE #90,D0
 JSR @DREHE
 MOVE #150,D0
 JSR @SCHREITE 
 MOVE #90,D0
 JSR @DREHE
 MOVE #60,D0
 JSR @SCHREITE 
 MOVE #90,D0
 JSR @DREHE
 MOVE #150+10,D0
 JSR @SCHREITE 
 MOVE #90,D0
 JSR @DREHE
 MOVE #60,D0
 JSR @SCHREITE 
 MOVE #-90,D0
 JSR @DREHE
 MOVE #-10,D0
 JSR @SCHREITE 
 MOVE #70,D0
 JSR @SCHREITE 
 MOVE #-90,D0
 JSR @DREHE
 MOVE #60,D0
 JSR @SCHREITE 
 MOVE #-90,D0
 JSR @DREHE
 MOVE #60,D0
 JSR @SCHREITE 
 RTS
 
* PROGRAMMBAUSTEIN
 
START:                  * HAUPTPROGRAMM
 BSR GEHAEUSE           * RAHMEN ZEICHNEN
 BSR ZIFFERBLATT        * UND ZIFFERNBLATT AUSGEBEN
WIEDERHOLE:
 MOVE #10-1,D4          * 10 MAL 1/10 SEKUNDE
WDH1:
 BSR SEK10TEL           * 1/10 SEK VERZOEGERN
 BSR ZAEPENDEL          * ZAEHLER UND
 DBRA D4,WDH1           * PENDEL DARSTELLEN
 BSR ZAEHLER            * DANN ZEIGER DARSTELLEN
 BRA WIEDERHOLE         * UNBEGRENZT
  
 END
 