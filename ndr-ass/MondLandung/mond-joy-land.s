;* MONDLANDEPROGRAMM MIT JOYSTICK
;* UND MONDFLAECHE MIT EBENEM LANDEPLATZ
 
 ORG $9C00
 
; ***** VEREINBARUNGS-BAUSTEIN *****
 
TASTEN EQU $FFFFFF30
RECHTS EQU 3
LINKS  EQU 2
RAUF   EQU 1
RUNTER EQU 0
FEUER  EQU 4
 
ERFOLGTEXT:
 DC.B 'Gut gelandet',0
 
BRUCHTEXT:
 DC.B 'Bruchlandung',0
 
FAEHRE:
 DC.B 0,1,1,0,0,0,0,0,7,7,0,4,3,3
 DC.B 2,4,4,4,4,4,6,2,0
 DC.B 3,2,2,2,1,0,0,0,7,6,6,6,5,10
 DS 0                   ; ANGLEICH AUF WORTLAENGE
 
FBREITE EQU 11*3        ; BREITE DER FAEHRE
 
LANDSCHAFT:             ; Y-KOORDINATEN, DER FLAECHE
                        ; DIE X-KOORDINATEN LAUFEN IN 10ER SCHRITTEN
 DC.W 0,10,15,25,45,40,50,35,30,35,50,65,60,60,50,40,25,20,20
 DC.W 25,40,50,45,40,40,40,40,40,40,45,40,30,45,60,75
 DC.W 75,60,50,35,30,20,30,15,20,15,25,10,15,5,0,0

ANZLAND EQU (*-LANDSCHAFT)/2     ; HALBE WEGEN WORT
 
; VARIABLENVEREINBARUNGEN
 
X: DC.W     0
Y: DC.W     0
DX: DC.W    0
DY: DC.W    0
TREIBST: DC.W   0

HX: DC.W     0
HY: DC.W     0
DHX: DC.W    0
DHY: DC.W    0

; ***** UNTERPROGRAMM-BAUSTEIN *****

MONDFLAECHE:
 CLR.L D1
 CLR.L D2
 JSR @MOVETO    ; DORT ANFANGEN
 LEA LANDSCHAFT,A0
 MOVE #ANZLAND-1,D3
MONDLP:
 MOVE (A0)+,D2
 JSR @DRAWTO
 ADD #10,D1
 DBRA D3,MONDLP
 RTS
 
LASER:          ; LASERSTRAHL-FORM
 MOVE HX,D1
 MOVE HY,D2
 JSR @MOVETO
 ADD #25,D1
 JSR @DRAWTO
 RTS
 
LASEREXEC:      ; LASER NEU AUSGEBEN
 TST HY
 BEQ WDH12
; LOESCHEN ALTE POS
 JSR @ERAPEN
 BSR LASER
 ADD #10,HX
 JSR @SETPEN
 BSR LASER
 CMP #512,HX
 BCS WDH12
 CLR HY
 CLR HX         ; STOP
WDH12:
*
 RTS
 
CHECKHOEHE:     ; Y IN ABHAENGIGKEIT VON X
                ; D0=FFFF, WENN OK, SONST 0
 MOVE X,D1
 BMI NOTOK      ; AUSSERHALB DES BILDSCHIRMS
 CMP #512,D1
 BGT NOTOK      ; IMMER BRUCH
 MOVE Y,D2
 BSR CHECK1     ; ZUERST LINKER AUFSETZPUNKT
 BEQ NOTOK      ; NEIN, DANN UNTER OBERFLAECHE
 MOVE X,D1
 MOVE Y,D2
 ADD #FBREITE,D1 ; NUN FUER RECHTE SEITE PRUEFEN
 BSR CHECK1
 RTS
 
CHECK1:         ; X=D1, Y=D2, PRUEFT HOEHE
 MOVE D1,D3     ; TABELLENEINTRAG FINDEN
 EXT.L D3       ; DAZU DURCH 10 DIVIDIEREN
 DIVU #10,D3    ; UND ALS WORT HOLEN
 LEA LANDSCHAFT,A1
 ADDA.W D3,A1
 ADDA.W D3,A1   ; 2* ADDIEREN, DA WORT
 MOVE (A1),D3   ; Y1
 SUB 2(A1),D3   ; Y1-Y2
 MOVE D1,D4 
 EXT.L D4
 DIVU #10,D4    ; ERST MAL X1 BERECHNEN
 MULU #10,D4
 ADD #10,D4     ; DA 10ER ABSTAND   =X2
 SUB D1,D4      ; IST X2-X
 MULS D4,D3     ; (Y2-Y1)*(X2-X)
 DIVS #10,D3    ; = YSA
 ADD 2(A1),D3   ; IST  YS  HOEHE DES SCHNITTPUNKTES
 CMP D2,D3
 BLE OKCHECK
 BRA NOTOK
 
OKCHECK:
 MOVE #$FFFF,D0
 RTS
NOTOK:
 CLR D0
 RTS
 
; ***** PROGRAMM-BAUSTEIN *****
START:
 BSR MONDFLAECHE
 MOVE #100,X
 MOVE #200,Y
 MOVE #3,DX
 MOVE #0,DY
 MOVE #300,TREIBST
 
WDH1:
 MOVEA.L #FAEHRE,A0
 MOVE #3,D0        ; VERGROESSERUNG=3
 MOVE X,D1
 MOVE Y,D2
 JSR @FIGUR
 TST TREIBST
 BLE NOTEST         ; KEIN TREIBSTOFF?
 MOVE.B TASTEN,D0
 BTST #RECHTS,D0
 BNE.S TST1
 ADDQ #3,DX
TST1: 
 BTST #LINKS,D0
 BNE.S TST2
 SUBQ #3,DX
TST2:
 BTST #RAUF,D0
 BNE.S TST3
 ADDQ #3,DY
TST3:
 BTST #RUNTER,D0
 BNE.S TST4
 SUBQ #3,DY
TST4:
 BTST #FEUER,D0
 BNE.S TST5
 TST HY
 BNE.S TST5
 MOVE X,HX
 MOVE Y,HY
 ADD #20,HX
 ADD #20,HY
 MOVE.W HY,D3
 ROR.W #$08,D3
 
TST5:
NOTEST:
 SUBQ #1,DY         ; GRAVITATION
 MOVE DX,D3         ; X:=X+DX
 ADD D3,X
 MOVE DY,D3         ; Y:=Y+DY
 ADD D3,Y
 SUBQ #1,TREIBST    ; TREIBSTOFF VERRINGERN
 MOVE #5-1,D1       ; WARTESCHLEIFE
WARTE20MS:
 JSR @SYNC
 BEQ WARTE20MS
 MOVE D1,-(A7)
 BSR LASEREXEC
 MOVE (A7)+,D1
 DBRA D1, WARTE20MS
 BSR CHECKHOEHE     ; HIER IN ABH. VON X PRUEFEN
 BNE WDH1           ; WENN <> 0 DANN OK
 MOVE #3,D0         ; SONST AM BODEN
 MOVE X,D1          ; FIGUR ABER ERST MAL DARSTELLEN
 MOVE Y,D2
 JSR @FIGUR
 JSR @SETFIG
 MOVE X,D0          ; PRUEFEN OB IM LANDEGEBIET
 CMP #230,D0        ; BEI UNS XMIN=230
 BLT BRUCH          ; NEIN, DANN BRUCH
 ADD #FBREITE,D0    ; ODER RECHTS ZU WEIT ?
 CMP #280,D0
 BGT BRUCH          ; DANN AUCH BRUCH
 MOVE DX,D0         ; BESCHLEUNIGUNG TESTEN
 BPL.S FF1
 NEG D0
FF1:                ; !DX! < 2
 CMP #2,D0
 BGE BRUCH
 MOVE DY,D0
 BPL FF2
 NEG D0
FF2:                ; !DY! < 4 
 CMP #4,D0
 BGE BRUCH
 LEA ERFOLGTEXT,A0
 BRA ENDE
 
BRUCH:
 LEA BRUCHTEXT,A0
ENDE:
 MOVE #$33,D0       ; SCHRIFTGROESSE
 CLR D1             ; X-POSITION
 MOVE #130,D2       ; Y-POSITION
 JSR @WRITE
 JSR @CI            ; WARTEN AUF TASTE
 JSR @CLR           ; LOESCHEN
 BRA START          ; UNBEGRENZT
 
; **********
 
END
