;*********************************
;* MONDLANDEPROGRAMM VERSION 1.0 ;
;* VOM 13.12.1983                ;
;*********************************

 ORG $A000

; ***** VEREINBARUNGS-BAUSTEIN *****

TASTEN EQU $FFFFFF30
RECHTS EQU 1            ; BIT MASKEN
LINKS  EQU 2
RAUF   EQU 4
RUNTER EQU 8

ERFOLGTEXT:
    DC.B    'Gut gelandet',0
BRUCHTEXT:
    DC.B    'Bruchlandung',0

FAEHRE:
 DC.b 8,2,9,0,0,7,3,2,2,4,4,4,4
 DC.b 6,6,5,1,0,0,10
 DS 0                   ; ANGLEICH AUF WORTLAENGE
 
; VARIABLENVEREINBARUNGEN
 
X: DC.W     0
Y: DC.W     0
DX: DC.W    0
DY: DC.W    0
TREIBST: DC.W   0
 
; ***** UNTERPROGRAMM-BAUSTEIN *****
 
MONDFLAECHE:
 MOVE #0,D1
 MOVE #0,D2
 MOVE #90,D3
 JSR @SET       ; SETZE SCHILDKROETE
 JSR @HIDE      ; ABER NICHT ZEIGEN
 MOVE #50-1,D4  ; ANZAHL KRATER
MLF1:
 JSR KRATER
 DBRA D4,MLF1
 RTS
 
KRATER:
 MOVE #-45,D0
 JSR @DREHE
 MOVE #10,D0
 JSR @SCHREITE
 MOVE #-45,D0
 JSR @DREHE
 MOVE #6,D0
 JSR @SCHREITE
 MOVE #-45,D0
 JSR @DREHE
 MOVE #10,D0
 JSR @SCHREITE
 MOVE #-45-180,D0
 JSR @DREHE
 RTS
 
; ***** PROGRAMM-BAUSTEIN *****
START:
 BSR MONDFLAECHE
 MOVE #100,X
 MOVE #200,Y
 MOVE #3,DX
 MOVE #0,DY
 MOVE #300,TREIBST
 
WDH1:
 MOVEA.L #FAEHRE,A0
 MOVE #$3,D0        ; BERGRIESSERUNG=3
 MOVE X,D1
 MOVE Y,D2
 JSR @FIGUR
 TST TREIBST
 BLE NOTEST         ; KEIN TREIBSTOFF?
 MOVE.B TASTEN,D0
 AND.B #RECHTS,D0
 BNE TST1
 ADDQ #3,DX
 BRA.S NOTEST
 
TST1: 
 MOVE.B TASTEN,D0
 AND.B #LINKS,D0
 BNE TST2
 SUBQ #3,DX
 BRA.S NOTEST
 
TST2:
 MOVE.B TASTEN,D0
 AND.B #RAUF,D0
 BNE TST3
 ADDQ #3,DY
 BRA.S NOTEST
 
TST3:
 MOVE.B TASTEN,D0
 AND.B #RUNTER,D0
 BNE NOTEST
 SUBQ #3,DY
 
NOTEST:
 SUBQ #1,DY         ; GRAVITATION
 MOVE DX,D3         ; X:=X+DX
 ADD D3,X
 MOVE DY,D3         ; Y:=Y+DY
 ADD D3,Y
 SUBQ #1,TREIBST    ; TREIBSTOFF VERRINGERN
 
 MOVE #5-1,D1       ; WARTESCHLEIFE
WARTE20MS:
; MOVE #!SYNC,D7
; TRAP #1
 JSR @SYNC
 BEQ WARTE20MS
 DBRA D1, WARTE20MS
 
 CMP #5,Y           ; HOEHE DER OBERFLAECHE UEBER
 BGT WDH1           ; BERÜCKSICHTIGEN
                    ;
 MOVE #$3,D0        ; FAEHRE AM SCHLUSS NOCHMALS
 MOVE X,D1
 MOVE Y,D2
 JSR @FIGUR
 MOVE DX,D0
 BPL.S FF1
 NEG D0
FF1:                ; !DX! < 2
 CMP #2,D0
 BGE.S BRUCH
 MOVE DY,D0
 BPL.S FF2
 NEG D0
FF2:                ; !DY! < 4 
 CMP #4,D0
 BGE.S BRUCH
 LEA ERFOLGTEXT,A0
 BRA.S ENDE
 
BRUCH:
 LEA BRUCHTEXT,A0
ENDE:
 MOVE #33,D0        ; SCHRIFTGROESSE
 MOVE #0,D1         ; X-POSITION
 MOVE #130,D2       ; Y-POSITION
 JSR @WRITE
 JSR @CI            ; WARTEN AUF TASTE
 JSR @CLR           ; LOESCHEN
 BRA START          ; UNBEGRENZT

; **********

END

    