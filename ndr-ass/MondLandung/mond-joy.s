;* MONDLANDEPROGRAMM MIT JOSYSTICK
;
 ORG $9C00

; ***** VEREINBARUNGS-BAUSTEIN *****

TASTEN EQU $FFFFFF30
RECHTS EQU 3
LINKS  EQU 2
RAUF   EQU 1
RUNTER EQU 0
FEUER  EQU 4

ERFOLGTEXT:
 DC.B 'Gut gelandet',0

BRUCHTEXT:
 DC.B 'Bruchlandung',0

FAEHRE:
 DC.B 0,1,1,0,0,0,0,0,7,7,0,4,3,3
 DC.B 2,4,4,4,4,4,6,2,0
 DC.B 3,2,2,2,1,0,0,0,7,6,6,6,5,10
 DS 0                   ; ANGLEICH AUF WORTLAENGE

; VARIABLENVEREINBARUNGEN

X: DC.W     0
Y: DC.W     0
DX: DC.W    0
DY: DC.W    0
TREIBST: DC.W   0

HX: DC.W     0
HY: DC.W     0
DHX: DC.W    0
DHY: DC.W    0

; ***** UNTERPROGRAMM-BAUSTEIN *****

MONDFLAECHE:
 CLR D1
 CLR D2
 MOVE #90,D3
 JSR @SET       ; SETZE SCHILDKROETE
 JSR @HIDE      ; ABER NICHT ZEIGEN
 MOVE #50-1,D4  ; ANZAHL KRATER
MLF1:
 JSR KRATER
 DBRA D4,MLF1
 RTS

KRATER:
 MOVE #-45,D0
 JSR @DREHE
 MOVE #10,D0
 JSR @SCHREITE
 MOVE #-45,D0
 JSR @DREHE
 MOVE #6,D0
 JSR @SCHREITE
 MOVE #-45,D0
 JSR @DREHE
 MOVE #10,D0
 JSR @SCHREITE
 MOVE #-45-180,D0
 JSR @DREHE
 RTS
;
LASER:          ; LASERSTRAHL-FORM
 MOVE HX,D1
 MOVE HY,D2
 JSR @MOVETO
 ADD #25,D1
 JSR @DRAWTO
 RTS

LASEREXEC:      ; LASER NEU AUSGEBEN
 TST HY
 BEQ WDH12
; LOESCHEN ALTE POS
 JSR @ERAPEN
 BSR LASERADD #10,HY
 JSR @SETPEN
 BSR LASER
 CMP #512,HX
 BCS WDH12
 CLR HY
 CLR HX         ; STOP
WDH12:
 RTS


; ***** PROGRAMM-BAUSTEIN *****
START:
 BSR MONDFLAECHE
 MOVE #100,X
 MOVE #200,Y
 MOVE #3,DX
 MOVE #0,DY
 MOVE #300,TREIBST
 
WDH1:
 MOVEA.L #FAEHRE,A0
 MOVE #3,D0        ; BERGRIESSERUNG=3
 MOVE X,D1
 MOVE Y,D2
 JSR @FIGUR
 TST TREIBST
 BLE NOTEST         ; KEIN TREIBSTOFF?
 MOVE.B TASTEN,D0
 BTST #RECHTS,D0
 BNE.S TST1
 ADDQ #3,DX
TST1: 
 BTST #LINKS,D0
 BNE.S TST2
 SUBQ #3,DX
TST2:
 BTST #RAUF,D0
 BNE.S TST3
 ADDQ #3,DY
 BRA.S NOTEST
TST3:
 BTST #RUNTER,D0
 BNE.S TST4
 SUBQ #3,DY
TST4:
 BTST #FEUER,D0
 BNE.S TST5
 MOVE X,HX
 MOVE Y,HY
 ADD #20,HX
 ADD #20,HY

TST5:
NOTEST:
 SUBQ #1,DY         ; GRAVITATION
 MOVE DX,D3         ; X:=X+DX
 ADD D3,X
 MOVE DY,D3         ; Y:=Y+DY
 ADD D3,Y
 SUBQ #1,TREIBST    ; TREIBSTOFF VERRINGERN
 MOVE #5-1,D1       ; WARTESCHLEIFE
WARTE20MS:
 JSR @SYNC
 BEQ WARTE20MS
 MOVE D1,-(A7)
 BSR LASEREXEC
 MOVE (A7)+,D1
 DBRA D1, WARTE20MS
 CMP #5,Y           ; HOEHE DER OBERFLAECHE UEBER
 BGT WDH1           ; BERÃœCKSICHTIGEN                    ;
 MOVE #$3,D0        ; FAEHRE AM SCHLUSS NOCHMALS
 MOVE X,D1
 MOVE Y,D2
 JSR @FIGUR
 MOVE DX,D0
 BPL.S FF1
 NEG D0
FF1:                ; !DX! < 2
 CMP #2,D0
 BGE.S BRUCH
 MOVE DY,D0
 BPL.S FF2
 NEG D0
FF2:                ; !DY! < 4 
 CMP #4,D0
 BGE.S BRUCH
 LEA ERFOLGTEXT,A0
 BRA.S ENDE

BRUCH:
 LEA BRUCHTEXT,A0
ENDE:
 MOVE #$33,D0       ; SCHRIFTGROESSE
 CLR D1             ; X-POSITION
 MOVE #130,D2       ; Y-POSITION
 JSR @WRITE
 JSR @CI            ; WARTEN AUF TASTE
 JSR @CLR           ; LOESCHEN
 BRA START          ; UNBEGRENZT
 
; **********

END
 
