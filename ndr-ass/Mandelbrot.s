* PROGRAMM ZUR DARSTELLUNG DER MANDELBROTMENGE
* ROLF-DIETER KLEIN    20.9.1985      1.0
* NACH "SCHOENHEIT IM CHAOS", BREMEN
*
* AUS LOOP 05, SEITE 16
 
* ALLE WERTE WERDEN MIT 1000 MULTIPLIZIERT ALS
* FESTKOMMAZAHL VERWENDET. DAMIT IST EINE
* SCHNELLE INTEGER-ARITHMETIK MOEGLICH.
 
PMIN EQU -2250      * WERTEBEREICH FÃœR DARSTELLUNG
PMAX EQU  750       * BILDAUSSCHNITT
QMIN EQU -1500      * MAX UND MIN MUESSEN
QMAX EQU  1500      * ZUSAMMEN EINE DIFFERENZ
                    * GROESSE 512 (256) ERGEBEN.
 
M EQU 16*1000*1000  * RADIUS*RADIUS FUER TEST
ANZAHL EQU 100      * MAX ITERATIONEN
 
START:
 MOVE #0,D1         * X=0
XSCHLEIFE:
 MOVE #0,D2         * Y=0
YSCHLEIFE:
 MOVE.L #PMAX-PMIN,D3
 MULS D1,D3         * MIT X-KOORD.
 DIVS #511,D3       * X-AUFLOESUNG
 ADD #PMIN,D3       * D3=P0 START
 MOVE.L #QMAX-QMIN,D4
 MULS D2,D4         * MIT Y-KOORD.
 DIVS #255,D4       * Y-AUFLOESUNG
 ADD #QMIN,D4       * D4=Q0 START
 CLR.L D5           * X0=0
 CLR.L D6           * Y0=0
 CLR D7             * K=0, ZAEHLER
 MOVEM.L D1-D2,-(A7)    * FREIRAUM SCHAFFEN
ITERAT:             * ITERATIONSSCHLEIFE
 MOVE D5,D1
 MULS D1,D1
 MOVE D6,D0
 MULS D0,D0
 SUB.L D0,D1
 DIVS #1000,D1
 ADD D3,D1          * XK+1 IN D1
 MOVE D5,D2
 MULS D6,D2
 DIVS #500,D2       * WEGEN FESTKOMMA ( UND * 2 )
 ADD D4,D2
 MOVE D1,D5
 MOVE D2,D6         * NEUE Y-WERTE
 ADD #1,D7          * NEUES K
 MULS D1,D1
 MULS D2,D2
 ADD.L D2,D1
 CMP.L #M,D1
 BGE AUSGABE
 CMP #ANZAHL,D7
 BEQ AUSGABE
 BRA ITERAT
AUSGABE:
 MOVEM.L (A7)+,D1-D2
 BTST #0,D7         * INDEXx
 BEQ WEITER
 JSR @MOVETO
 MOVE.B #$80,D0
 JSR @CMD           * PUNKT AUSGEBEN
WEITER:
 ADD #1,D2          * NEUES Y
 CMP #256,D2
 BLT YSCHLEIFE
 ADD #1,D1          * NEUES X
 CMP #512,D1
 BLT XSCHLEIFE
WARTE:
 JSR @CI            * WARTEN BIS TASTE
 CMP.B #'m',D0      * m  GEDRUECKT.
 BNE WARTE
 RTS
